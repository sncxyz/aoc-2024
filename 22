import run;

fn main() run.day 22 parse part_1 part_2;

fn parse(input) input split "\n" map int.1;

fn part_1(nums) nums
    map (\(n) 0 to 2000 fold n \(n, _) succ(n))
    sum;

fn part_2(nums) {
    totals := [0] * 130321;
    for n in nums {
        seen := [false] * 130321;
        changes := [0] * 4;
        digit := n mod 10;
        for i in 0 to 2000 {
            n = succ(n);
            d := n mod 10;
            changes[i & 3] = d - digit + 9;
            digit = d;
            if i >= 3 {
                changes := get_id(changes, i);
                if not seen[changes] {
                    seen[changes] = true;
                    totals[changes] += digit;
                }
            }
        }
    }
    return totals seq_max;
}

fn succ(num) num -> left 6 -> prune -> right 5 -> left 11 -> prune;

fn left(num, x) num << x xor num;

fn right(num, x) num >> x xor num;

fn prune(num) num & 16777215;

fn get_id(changes, i) changes[i & 3]
    + (changes[i + 1 & 3] * 19)
    + (changes[i + 2 & 3] * 361)
    + (changes[i + 3 & 3] * 6859);
